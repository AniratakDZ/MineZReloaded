plugins {
    id 'java'
}

allprojects {
    group = 'de.jefa'

    repositories {
        mavenCentral()
        maven {
            name = "papermc"
            url = uri("https://repo.papermc.io/repository/maven-public/")
        }
    }
}

subprojects {
    apply plugin: 'java'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(22)
        }
    }
}

// --- Automatische Version aus letztem Git-Tag + Commit-Count ---
def stdout = new ByteArrayOutputStream()
exec {
    commandLine 'git', 'describe', '--tags', '--abbrev=0'
    standardOutput = stdout
    ignoreExitValue = true
}
def lastTag = stdout.toString().trim()
if (!lastTag) {
    lastTag = "0.0.0"
}

def commitCount = 'git rev-list --count HEAD'.execute().text.trim()

version = "${lastTag}.${commitCount}"
println "Building version: $version"

// JAR-Dateien bekommen automatisch den Versionsnamen
subprojects {
    tasks.withType(Jar) {
        archiveBaseName.set(project.name)
        archiveVersion.set(project.version)
    }
}